<?xml version="1.0"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:exslt="http://exslt.org/common"
    xmlns:math="http://exslt.org/math"
    xmlns:date="http://exslt.org/dates-and-times"
    xmlns:func="http://exslt.org/functions"
    xmlns:set="http://exslt.org/sets"
    xmlns:str="http://exslt.org/strings"
    xmlns:dyn="http://exslt.org/dynamic"
    xmlns:saxon="http://icl.com/saxon"
    xmlns:xalanredirect="org.apache.xalan.xslt.extensions.Redirect"
    xmlns:xt="http://www.jclark.com/xt"
    xmlns:libxslt="http://xmlsoft.org/XSLT/namespace"
    xmlns:test="http://xmlsoft.org/XSLT/"
    extension-element-prefixes="exslt math date func set str dyn saxon xalanredirect xt libxslt test"
    exclude-result-prefixes="math str">
    <xsl:output method="text" indent="no"/>
    <xsl:param name="workingDirectory">LIT_MODEL</xsl:param>
    
    <!--                                                                   -->
    <!-- This xslt transform one xml settings file into                    --> 
    <!-- one LITpro description                                            -->
    <!-- Developpers can consider the schema file of xml settings files    -->
    <!--                                                                   -->


    <!--                               MAIN TEMPLATE                       -->
    <xsl:template match="/">
        <xsl:value-of select="'/* This settings file has been generated by the LITpro GUI for the yorick environment */&#10;&#10;'"/>

        <xsl:value-of select="'_LPP_MODEL_SETTINGS = h_new(&#10;'"/>

        <xsl:call-template name="targetSection"/>
        <xsl:value-of select="',&#10;'"/>
        <xsl:call-template name="paramSection"/>
        <xsl:value-of select="',&#10;'"/>
        <xsl:call-template name="fitterSection"/>
        <xsl:value-of select="'&#10;'"/>

        <xsl:value-of select="');&#10;'"/>


        <xsl:call-template name="customModelsSection"/>
        <xsl:value-of select="'&#10;'"/>

        <!-- create one .b64 file per hrefed data file or plot-->
        <!-- <xsl:if test="$createfiles='yes'">
            <xsl:for-each select="//files/file[@href]">
            <xsl:document href="{./@name}.b64" method="text"
                xml:space="preserve"><xsl:value-of select="substring-after(./@href,'base64,')"/></xsl:document>
        </xsl:for-each>
        <xsl:for-each select="//resultFile[@href]">
            <xsl:document href="{./@name}.b64" method="text"
                xml:space="preserve"><xsl:value-of select="substring-after(./@href,'base64,')"/></xsl:document>
        </xsl:for-each>
        </xsl:if>-->

    </xsl:template>

    <!-- targetSection                                                  -->
    <xsl:template name="targetSection">
        <xsl:value-of select="'  target = h_new(&#10;'"/>
        <xsl:for-each select="//targets/target">
            <!-- target definition -->
            <xsl:value-of select="'    TG'"/>
            <xsl:value-of select="position()"/>
            <xsl:value-of select="' = h_new(&#10;'"/>

            <!-- ident definition -->
            <xsl:value-of select="'      ident = &quot;'"/>
            <xsl:value-of select="./ident"/>
            <xsl:value-of select="'&quot;,&#10;'"/>

            <!-- file definition -->
            <xsl:value-of select="'      files = ['"/>
            <xsl:for-each select=".//fileLink">
                <xsl:value-of select="'&quot;'"/>
                <xsl:variable name="fileRef"><xsl:value-of select="./@fileRef"/></xsl:variable>
                <xsl:value-of select="//file[./@id=$fileRef]/@name"/>
                <xsl:value-of select="'&quot;'"/>
                <xsl:if test="not(position() = last())">
                    <xsl:value-of select="','"/>
                </xsl:if>
            </xsl:for-each>
            <xsl:value-of select="'],&#10;'"/>
            <!-- object definition -->
            <xsl:for-each select=".//model">
                <xsl:variable name="modelPosition" select="position()"/>
                <xsl:value-of select="'      OBJ'"/>
                <xsl:value-of select="$modelPosition"/>
                <xsl:value-of select="' = h_new(&#10;'"/>
                <!-- name definition -->
                <xsl:value-of select="'        name = &quot;'"/>
                <xsl:if test="./@polar='1' or ./@polar='true'">polar_</xsl:if>
                <xsl:if test="./@stretched='1' or ./@stretched='true'">stretched_</xsl:if>
                <xsl:value-of select="./@type"/>
                <xsl:value-of select="'&quot;,&#10;'"/>
                <!-- params definition -->
                <xsl:value-of select="'        params = h_new('"/>
                <xsl:variable name="mySharedParameters">
                    <xsl:for-each select=".//parameterLink">
                        <xsl:variable name="paramId" select="./@parameterRef"/>
                        <xsl:variable name="sharedParam" select="//parameters/parameter[@id=$paramId]"/>
                        <xsl:value-of select="./@type"/>
                        <xsl:value-of select="' = &quot;'"/>
                        <xsl:value-of select="$sharedParam/@name"/>
                        <xsl:value-of select="'&quot;'"/>
                        <xsl:if test="not(position() = last()) or ../parameter">
                            <xsl:value-of select="', '"/>
                        </xsl:if>
                    </xsl:for-each>
                </xsl:variable>
                <xsl:value-of select="$mySharedParameters"/>
                <xsl:for-each select=".//parameter">
                    <xsl:value-of select="./@type"/>
                    <xsl:value-of select="' = &quot;'"/>
                    <xsl:value-of select="./@name"/>
                    <xsl:value-of select="'&quot;'"/>
                    <xsl:if test="not(position() = last())">
                        <xsl:value-of select="', '"/>
                    </xsl:if>
                </xsl:for-each>
                <xsl:value-of select="')&#10;'"/>
                <xsl:value-of select="'      ),&#10;'"/>
            </xsl:for-each>

            <!-- residual definition -->
            <xsl:value-of select="'      residuals = h_new(&#10;'"/>
            <xsl:for-each select=".//residual">
                <xsl:value-of select="'          '"/>
                <xsl:value-of select="@name"/>
                <xsl:value-of select="' = &quot;'"/>
                <xsl:value-of select="@type"/>
                <xsl:value-of select="'&quot;,&#10;'"/>
            </xsl:for-each>



            <xsl:choose>
                <xsl:when test="contains(./normalize,'false') or
                    contains(./normalize,'0')">
                    <xsl:value-of select="'        NORMALIZE = FALSE&#10;'"/>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="'        NORMALIZE = TRUE&#10;'"/>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:value-of select="'      )&#10;'"/>


            <xsl:value-of select="'    )&#10;'"/>
            <!-- add , if target is not the last -->
            <xsl:if test="not(position() = last())">
                <xsl:value-of select="', '"/>
            </xsl:if>
        </xsl:for-each>
        <xsl:value-of select="'  )'"/>
    </xsl:template>

    <!-- paramSection                                                  -->
    <xsl:template name="paramSection">
        <xsl:value-of select="'  param = h_new(&#10;'"/>
        <!-- List parameters -->
        <xsl:for-each select="//model/parameter | //parameters/parameter">
            <xsl:apply-templates select="." mode="forParamSection"/>
            <xsl:if test="not(position() = last())"><xsl:value-of select="','"/></xsl:if>
            <xsl:value-of select="'&#10;'"/>
        </xsl:for-each>
        <xsl:value-of select="'  )'"/>
    </xsl:template>

    <xsl:template match="parameter" mode="forParamSection">
        <xsl:param name="parameter" select="."/>
        <xsl:value-of select="'    '"/>
        <xsl:value-of select="$parameter/@name"/>
        <xsl:value-of select="' = h_new('"/>
        <xsl:value-of select="'value='"/>
        <xsl:value-of select="$parameter/value"/>
        <xsl:value-of select="', '"/>
        <xsl:value-of select="'vmin='"/>
        <xsl:value-of select="$parameter/minValue"/>
        <xsl:value-of select="', '"/>
        <xsl:value-of select="'vmax='"/>
        <xsl:value-of select="$parameter/maxValue"/>
        <xsl:value-of select="', '"/>
        <xsl:value-of select="'units=&quot;'"/>
        <xsl:value-of select="$parameter/units"/>
        <xsl:value-of select="'&quot;'"/>
        <xsl:if test="contains($parameter/hasFixedValue,'true') or contains($parameter/hasFixedValue,'1')">
        <xsl:value-of select="', flags=FIXED'"/>
        </xsl:if>
        <xsl:if test="string-length($parameter/scale)&gt;0">
        <xsl:value-of select="concat(', scale=',$parameter/scale)"/>
        </xsl:if>
        <xsl:value-of select="')'"/>
    </xsl:template>


    <!-- fitterSection                                                  -->
    <xsl:template name="fitterSection">
        <xsl:value-of select="'  fitter = &quot;'"/>
        <xsl:value-of select="//fitter"/>
        <xsl:value-of select="'&quot;'"/>
    </xsl:template>
    
    <!-- customModelsSection                                                  -->
    <xsl:template name="customModelsSection">        
    
    <xsl:value-of select="'&#10;'"/>
    <xsl:value-of select="//usercode/common"/>
    
    <xsl:for-each select="//model[code]">
            <xsl:value-of select="'&#10;'"/>
            <!-- routine prototype -->
            <xsl:value-of select="concat('func ',@type, '(ufreq, vfreq,' )"/>
            <xsl:for-each select="./parameter">
                <xsl:value-of select="@type"/>
                <xsl:if test="position()!=last()">
                    <xsl:value-of select="', '"/>
                </xsl:if>
            </xsl:for-each>
            <xsl:value-of select="')&#10;'"/>
            
            <!-- routine core -->
            <xsl:value-of select="'{&#10;'"/>
            <xsl:value-of select="code"/>            
            <xsl:value-of select="'&#10;}&#10;'"/>
            
            <xsl:value-of select="'&#10;'"/>            
        </xsl:for-each>        
    
    <xsl:for-each select="//model[./@polar='1' or ./@polar='true' or ./@stretched='1' or ./@stretched='true' ]">

            <xsl:variable  name="transformUV" select="./@stretched='1' or ./@stretched='true'"/>

            <xsl:value-of select="'&#10;'"/>
            <!-- routine prototype -->
            <xsl:value-of select="'func '"/> 
            <xsl:for-each select="@polar | @stretched">
                <xsl:if test=".='1' or .='true'">
                    <xsl:value-of select="concat(name(.),'_')"/>
                </xsl:if>
            </xsl:for-each>
            <xsl:value-of select="concat(@type, '(ufreq, vfreq,' )"/>
            <xsl:for-each select="./parameter">
                <xsl:value-of select="@type"/>
                <xsl:if test="position()!=last()">
                    <xsl:value-of select="', '"/>
                </xsl:if>
            </xsl:for-each>
            <xsl:value-of select="')&#10;'"/>
            <!-- routine core -->
            <xsl:value-of select="'{&#10;'"/>
            
            <xsl:if test="./@polar='1' or ./@polar='true'">
            <xsl:text>    lw_xy = lp_rho_PA_to_xy(rho, theta); </xsl:text>
            </xsl:if>
            
            <xsl:if test="$transformUV">
            <xsl:value-of select="'&#10;    T_uv =_lpb_transform(ufreq,vfreq,stretched_ratio,1.,rotation);'" />
            </xsl:if>
            <xsl:value-of select="concat('&#10;    return lpb_',@type, '(')"/>
            <xsl:choose>
            <xsl:when test="$transformUV">T_uv(.., 1), T_uv(.., 2), </xsl:when>
            <xsl:otherwise>ufreq, vfreq, </xsl:otherwise>
            </xsl:choose>
            
            <xsl:for-each select="./parameter">
                <xsl:choose>
                <xsl:when test="@type='rho'">lw_xy(1)</xsl:when>
                <xsl:when test="@type='theta'">lw_xy(2)</xsl:when>
                <xsl:when test="@type='rotation'"></xsl:when>
                <xsl:when test="@type='stretched_ratio'"></xsl:when>
                <xsl:otherwise>
                <xsl:value-of select="@type"/>
                </xsl:otherwise>
                </xsl:choose>
                <!--<xsl:if test="position()!=last() and not(./following-sibling::parameter[position()=first() and @type='rotation'])"> <xsl:value-of select="', '"/> </xsl:if>-->
                <xsl:if test="position()!=last() and not(./following-sibling::parameter[1]/@type='stretched_ratio' or ./following-sibling::parameter[1]/@type='rotation')"> <xsl:value-of select="', '"/> </xsl:if>
            </xsl:for-each>
            <xsl:value-of select="');&#10;}&#10;'"/>
        </xsl:for-each>

    </xsl:template>

</xsl:stylesheet>
